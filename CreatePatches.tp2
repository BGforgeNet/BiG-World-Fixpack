// BiG World Fixpack Create Patches Script
//
// To use this script:
//   1. Copy this tp2 file to your game folder and make a copy of WeiDU.exe named Setup-CreatePatches.exe
//   2. In your game directory, create a new folder called BWP_Fixpack_Prepatch/
//   3. Unpack into that folder the original files of any mods for which you want to generate patches
//   4. On Windows, also copy into that folder the Fixpack's '_diffgawk' subfolder (contains diff.exe, gawk.exe, libiconv2.dll, libintl3.dll)
//   5. Run Setup-CreatePatches.exe in your game directory
//
// You can re-run this script as many times as you want.  It will not leave a record in the WeiDU.log.
//
// For each folder name found in BWP_Fixpack_Prepatch/, if there is a matching folder name in your game directory, this script will
// generate patches in BWP_Fixpack_Prepatch/BWFIX/[mod_folder] that reflect any changes necessary to convert BWP_Fixpack_Prepatch/[mod_folder]
// to match your game_directory/[mod_folder]: i.e., the version in your game directory is the desired result of patching the one in BWP_Fixpack_Prepatch/
//
// The script will also look for a setup-[mod_folder].tp2 or [mod_folder].tp2 file in your game folder, and if found, will look for a tp2 file
// with the same name in BWP_Fixpack_Prepatch/.  If there is a match, the script will create a patch for the tp2 file in BWP_Fixpack_Prepatch/BWFIX/
// (note: the tp2 patch will be outside the BWP_Fixpack_Prepatch/BWFIX/[mod_folder] in this case).
//
//     If a tp2 is found in your game directory but you forgot to provide the original tp2 in BWP_Fixpack_Prepatch/, this script will stop with an error
//
// The script will ignore mod folders that have spaces in the name, and will ask you about mods that have been processed by the script on previous runs.
//
// The script will NOT compare tp2 files in the game folder whose mod_folder is different from the tp2 (e.g., Setup-RoT.tp2 vs. RoTerror folder)
//     As a workaround, temporarily rename the tp2 file to be the same as the folder name, then manually correct the path in the generated tp2 patch file
//     It is better to rename the tp2, not the mod folder itself, because any patch files generated will have the wrong path; one file to fix vs. many

BACKUP ~BWP_Fixpack/backup~ // not the same folder as the one containing Fixpack patches
AUTHOR ~agb1 on shsforums.net, gibberlings3.net or forums.beamdog.com~

VERSION ~v0.2~

ALWAYS
	OUTER_SPRINT PREPATCH ~BWP_Fixpack_Prepatch~
	OUTER_SPRINT ALREADY_PROCESSED_FILE ~%PREPATCH%/AlreadyProcessed.txt~

	// platform specific syntax and utilities
	ACTION_IF (~%WEIDU_OS%~ STR_EQ ~win32~) THEN BEGIN
		OUTER_TEXT_SPRINT DIFF_CMD ~%PREPATCH%\diffgawk\diff.exe -u --strip-trailing-cr --ignore-file-name-case~
		OUTER_TEXT_SPRINT GAWK_CMD ~%PREPATCH%\diffgawk\gawk.exe~
		ACTION_IF (NOT FILE_EXISTS ~%PREPATCH%\diffgawk\diff.exe~ OR NOT FILE_EXISTS ~%PREPATCH%\diffgawk\gawk.exe~) THEN BEGIN
			FAIL ~Copy the 'diffgawk' folder from Fixpack _utils to %PREPATCH%/ before running this script~
		END
	END ELSE BEGIN // Linux/OSX
		OUTER_TEXT_SPRINT DIFF_CMD ~diff -u --strip-trailing-cr --ignore-file-name-case~
		OUTER_TEXT_SPRINT GAWK_CMD ~gawk~
	END
	
	DEFINE_ACTION_FUNCTION ~process_mod_folders~
	BEGIN
		GET_DIRECTORY_ARRAY ~folder_array~ ~%PREPATCH%~ ~^.*$~
		ACTION_PHP_EACH ~folder_array~ AS index => folder BEGIN
			ACTION_IF (~%folder%~ STRING_MATCHES_REGEXP ~^.*/\.\.?$~ != 0) THEN BEGIN // exclude /. and /..
				LAF FILE_OF_FILESPEC // trim leading ./
					STR_VAR
						filespec = EVAL ~%folder%~
					RET
						mod_folder = file
				END

				OUTER_SET skip = 0
				ACTION_IF (~%folder%~ STRING_CONTAINS_REGEXP ~ ~ == 0) THEN BEGIN // skip directories with spaces
					PRINT ~Skipping "%folder%" because it contains spaces...~
					OUTER_SET skip = 1
				END ELSE ACTION_IF (FILE_CONTAINS_EVALUATED (~%ALREADY_PROCESSED_FILE%~ ~Processed '%mod_folder%'~)) THEN BEGIN
					PRINT ~@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ %WNL%~ ^ // %WNL% = Windows New Line, ^ = concatenate with next
					      ~Do you want to re-generate patches for "%mod_folder%"? %WNL%~ ^
						  ~Enter Y if yes, anything else to skip. %WNL%~ ^
						  ~@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@~
					ACTION_READLN answer
					ACTION_IF NOT (~%answer%~ STR_EQ ~y~) THEN BEGIN // STR_EQ matches 'y' or 'Y'
						PRINT ~Skipping "%mod_folder%" because it is in %ALREADY_PROCESSED_FILE%...~
						OUTER_SET skip = 1
					END
				END ELSE ACTION_FOR_EACH reserved IN ~data~ ~lang~ ~Manuals~ ~movies~ ~mpsave~ ~music~ ~override~ ~save~ ~scripts~ ~%PREPATCH%~ ~diffgawk~ ~BWFIX~ BEGIN
					ACTION_IF (~%reserved%~ STR_EQ ~%mod_folder%~) THEN BEGIN // skip reserved folder names
						//PRINT ~Skipping "%mod_folder%" because the name is reserved...~
						OUTER_SET skip = 1
					END
				END
				
				ACTION_IF (NOT skip AND DIRECTORY_EXISTS ~%mod_folder%~) THEN BEGIN
					OUTER_SPRINT ~output_dir~ ~%PREPATCH%/BWFIX/%mod_folder%~
					OUTER_SPRINT ~tmp_patch~ ~%output_dir%/combined.patch~
					
					ACTION_IF (DIRECTORY_EXISTS ~%output_dir%~) THEN BEGIN
						PRINT ~Clearing old patch files from %output_dir%...~
						DELETE + ~%output_dir%~
					END
					PRINT ~Processing %mod_folder%...~

					//PRINT "MKDIR ~%output_dir%~"
					MKDIR ~%output_dir%~
					
					// special case - create patch for setup tp2 if it is in game folder instead of in mod folder
					ACTION_FOR_EACH tp2_file IN ~%mod_folder%.tp2~ ~setup-%mod_folder%.tp2~ BEGIN
						ACTION_IF (FILE_EXISTS ~%tp2_file%~) THEN BEGIN // if it is in the game folder...
							ACTION_IF (FILE_EXISTS ~%PREPATCH%/%tp2_file%~) THEN BEGIN // and also in the %PREPATCH% folder...
								//PRINT "AT_NOW ~%DIFF_CMD% %PREPATCH%/%tp2_file% %tp2_file% > %PREPATCH%/BWFIX/%tp2_file%.patch~"
								AT_NOW ~%DIFF_CMD% %PREPATCH%/%tp2_file% %tp2_file% > %PREPATCH%/BWFIX/%tp2_file%.patch~ // compare old version (in %PREPATCH%) to version in game folder
								
								ACTION_IF (FILE_SIZE ~%PREPATCH%/BWFIX/%tp2_file%.patch~ 0) THEN BEGIN
									DELETE + ~%PREPATCH%/BWFIX/%tp2_file%.patch~ // delete empty patch - no difference in tp2 files
								END
							END ELSE BEGIN
								FAIL ~%tp2_file% found in game folder but not in %PREPATCH% folder~
							END
						END
					END

					// generate remaining patches recursively (-r) and save to temporary file (one file containing all patches)
					//PRINT "AT_NOW ~%DIFF_CMD% -r %PREPATCH%/%mod_folder% %mod_folder% > %tmp_patch%~"
					AT_NOW ~%DIFF_CMD% -r %PREPATCH%/%mod_folder% %mod_folder% > %tmp_patch%~ // compare old version (in %PREPATCH%) to version in game folder

					MOVE + ~%tmp_patch%~ ~%tmp_patch%.bak~ // need to rename so we can overwrite
					COPY + ~%tmp_patch%.bak~ ~%tmp_patch%~ // read and modify combined patch file
						REPLACE_TEXTUALLY    ~^--- .*%mod_folder%\/~  ~--- %mod_folder%/~         // standardize --- path in patch
						REPLACE_TEXTUALLY ~^\+\+\+ .*%mod_folder%\/~  ~+++ Fixpack/%mod_folder%/~ // standardize +++ path in patch
						
						COUNT_2DA_ROWS 4 rows // look for lines with 4 columns - diff output matching "Only in path: filename"
						FOR (i=0; i < rows; i=i+1) BEGIN
							READ_2DA_ENTRY i 0 4 only
							READ_2DA_ENTRY i 1 4 in
							READ_2DA_ENTRY i 2 4 path
							READ_2DA_ENTRY i 3 4 filename
							INNER_ACTION BEGIN
								ACTION_IF (~%only%~ STR_EQ ~Only~ AND ~%in%~ STR_EQ ~in~ AND ~%path%~ STRING_CONTAINS_REGEXP ~backup:$~) THEN // ignore backup folder
								BEGIN
									//PRINT "%only% %in% %path% %filename%"
									ACTION_IF ( (0 == (~%filename%~ STRING_CONTAINS_REGEXP ~^fl#utf8~)) OR (0 == (~%filename%~ STRING_CONTAINS_REGEXP ~\.rej\b~)) ) THEN
									BEGIN // ignore UTF8 converted tra files and failed patches
										//PRINT ~Skipping file~
									END ELSE BEGIN
										OUTER_PATCH_SAVE relpath ~%path%~ BEGIN // get relative path for _copy / _delete
											REPLACE_TEXTUALLY ~%mod_folder%~ ~~    // strip mod folder (always)
											REPLACE_TEXTUALLY ~%PREPATCH%[\\/]~ ~~       // strip %PREPATCH% and and trailing slash (only when _delete)
											REPLACE_TEXTUALLY ~:~ ~~               // strip ':' (always) and trailing slash (only when _delete)
											// example 1:  %PREPATCH%/mod_folder/a/b/c: -> %PREPATCH%/a/b/c: -> a/b/c: -> a/b/c
											// example 2:  %PREPATCH%/mod_folder: -> %PREPATCH%/: -> : -> empty string
											// example 3:  mod_folder/a/b/c: -> /a/b/c: -> /a/b/c
											// example 4:  mod_folder: -> : -> empty string
										END
										ACTION_IF (NOT FILE_EXISTS ~%PREPATCH%/%mod_folder%%relpath%/%filename%~)
										BEGIN  // if version in %PREPATCH% folder doesn't have file -> _copy (relpath will be blank or have leading slash)
											ACTION_IF (NOT DIRECTORY_EXISTS ~%output_dir%/_copy%relpath%~) THEN BEGIN
												//PRINT "MKDIR ~%output_dir%/_copy%relpath%~"
												MKDIR ~%output_dir%/_copy%relpath%~
											END
											PRINT ~Copying "%mod_folder%%relpath%/%filename%" to "%output_dir%/_copy%relpath%"~
											COPY + ~%mod_folder%%relpath%/%filename%~ ~%output_dir%/_copy%relpath%~
										END ELSE
										BEGIN  // if version in game folder doesn't have file -> _delete (relpath will be blank or no leading slash)
											OUTER_PATCH_SAVE delpath ~%relpath%/~ BEGIN // add trailing slash
												REPLACE_TEXTUALLY ~^\/~ ~~ // if was empty string, return to empty string (remove slash just added)
											END
											ACTION_IF (NOT FILE_EXISTS ~%output_dir%/_delete~) THEN BEGIN
												COPY ~.../BWP_Fixpack-inlined/empty_file~ ~%output_dir%/_delete~ // create _delete file from inlined empty file
											END
											PRINT ~Appending "%delpath%%filename%" to "%output_dir%/_delete"~
											APPEND_OUTER + ~%output_dir%/_delete~ ~%delpath%%filename%~
										END
									END
								END
							END
						END
					
					PRINT ~Writing patches for any files inside the mod directory that are different...~
					// splitting combined patch file into individual patch files
					AT_NOW ~%GAWK_CMD% "/^(-|\+| |@)/ { if ( match($0, /^--- (.+\/)?([^\t]+)/, m) ) { f = \"%PREPATCH%/BWFIX/%mod_folder%/\" m[2] \".patch\" ; print f } ; print $0 > f }" %tmp_patch%~ EXACT
					// ^--- (.+\/)? captures leading path, if any, and ([^\t]+) captures the filename, without slashes or path, as match group 2
					// example 1:  --- Angelo/Dialogue/ADAng25J.d	2008-05-13 02:59:02.000000000 -0400
					// example 2:  --- Setup-Angelo.tp2	2008-05-13 02:59:02.000000000 -0400
					DELETE + ~%tmp_patch%~ // delete combined patch file after splitting
					DELETE + ~%tmp_patch%.bak~ // delete combined patch file after splitting
					
					APPEND_OUTER + ~%ALREADY_PROCESSED_FILE%~ ~Processed '%mod_folder%'~ UNLESS ~Processed '%mod_folder%'~
				END // processing mod folder
			END
		END
	END
END // ALWAYS

BEGIN ~Create Patches~ INSTALL_BY_DEFAULT NO_LOG_RECORD

ACTION_IF (NOT DIRECTORY_EXISTS ~%PREPATCH%~) THEN BEGIN
	MKDIR ~%PREPATCH%~
	FAIL ~%PREPATCH% folder created.  Unpack pre-patch mod folders and setup-tp2 files into that folder before running this script!~
END

ACTION_IF (NOT DIRECTORY_EXISTS ~%PREPATCH%/BWFIX~) THEN BEGIN
	MKDIR ~%PREPATCH%/BWFIX~ // output folder for patches
END

<<<<<<<< .../BWP_Fixpack-inlined/empty_file
>>>>>>>>

ACTION_IF (NOT FILE_EXISTS ~%ALREADY_PROCESSED_FILE%~) THEN BEGIN
	COPY ~.../BWP_Fixpack-inlined/empty_file~ ~%ALREADY_PROCESSED_FILE%~
END

LAUNCH_ACTION_FUNCTION ~process_mod_folders~ END // for each "original" mod folder in %PREPATCH%/, if there is an identically named mod folder in ./,
                                                 //   generate patches in %PREPATCH%/BWFIX/mod_folder/
                                                 // be careful, don't create a %PREPATCH%/override/ or %PREPATCH%/data/!
